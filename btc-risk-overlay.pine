// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Inspired by Benjamin Cowen's BTC risk idea. Special thanks to BitcoinRaven for working out most of the oscillators involved.
// Overlay version
// Â© Nick Petalas

//@version=5
indicator("Risk Metric: BTCUSD Overlay", max_bars_back = 5000, overlay=true)

var int startDate = na
if barstate.isfirst
    startDate := time

daysSinceGenesis(t) => math.floor((t - timestamp('2009-01-03'))/1000/60/60/24)

normalize(data) =>
    var min =  10e10
    var max = -10e10
    min := math.min(nz(data, min), min)
    max := math.max(nz(data, max), max)
    (data - min) / (max - min)

linReg(data) =>
    numBars = ta.barssince(time <= startDate) 
    len = numBars > 0 ? numBars : 1 
    reg = ta.linreg(data, len, 0) 
    
src = input.source(ohlc4, title="Source")

// experimental
normalizeStrategy(data, smoothen = false) =>
    d = smoothen ? ta.sma(data, 5) : data
    normalize(d)
    
///// DAILY_OVER_WEEKLY_EMA ///// 
lengthD = input(5, title="DMA Period Length (50 for smoother slopes, 5 for precision and volatility)")
lengthM = input(50, title="WMA Period Length (50 recommended)")
daily = request.security(syminfo.tickerid, "D", ta.ema(src, lengthD))
weekly = request.security(syminfo.tickerid, "W", ta.ema(src, lengthM))
DAILY_OVER_WEEKLY_EMA = normalizeStrategy(daily / weekly)

///// MAYER_MULTIPLE ///// 
mm = src / ta.sma(src, 200)
MAYER_MULTIPLE = normalizeStrategy(mm)

///// SHARPE /////
s_len = 356
ret = ta.change(src)
s = (ta.sma(ret, s_len) - 1) / ta.stdev(ret, s_len)
SHARPE = normalizeStrategy(s)

///// RISK_400_MA /////
RISK_400_MA = normalizeStrategy(src / ta.ema(src, 400))

///// RSI_20 /////
RSI = normalizeStrategy(ta.rsi(src, 20))

///// PUELL_MULTIPLE /////
btcIssuance = 7200 / math.pow(2, math.floor((daysSinceGenesis(time)-bar_index)/1458)) // likely wrong
usdIssuance = btcIssuance * src
MAusdIssuance = ta.sma(usdIssuance, 365)
pm = usdIssuance / MAusdIssuance
PUELL_MULTIPLE = normalizeStrategy(pm)

///// AVG ///// 
AVG = (DAILY_OVER_WEEKLY_EMA + MAYER_MULTIPLE + SHARPE + RISK_400_MA + RSI + PUELL_MULTIPLE) / 6.0

adjustAvg(data) =>
    lr = linReg(src)
    multi = math.log(lr) * math.log(ta.atr(5)) // feels too arbitrary, open to suggestions
    data * multi

    
ADJ_AVG_NORM = normalizeStrategy(adjustAvg(AVG), true)

///// JET GRADIENT APPROXIMATION /////
blue    = #0000FFFF
cyan    = #00FFFFFF
green   = #00FF00FF
yellow  = #FFFF00FF
red     = #FF0000FF

avgColor = ADJ_AVG_NORM <= 0.25 ? color.from_gradient(ADJ_AVG_NORM, 0.00, 0.25, blue, cyan) : 
           ADJ_AVG_NORM <= 0.50 ? color.from_gradient(ADJ_AVG_NORM, 0.25, 0.50, cyan, green) : 
           ADJ_AVG_NORM <= 0.75 ? color.from_gradient(ADJ_AVG_NORM, 0.50, 0.75, green, yellow) : 
           color.from_gradient(ADJ_AVG_NORM, 0.75, 1, yellow, red)

///// DRAW A THICK LINE ON TOP OF THE BARS /////
plot(src, "", avgColor, 6)
